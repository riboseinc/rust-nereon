whitespace = _{ " " }
sep = _{ "," | "\n" | "\r" }
infix = { "+" | "-" | "*" | "/" | "%" | "\\" | "^" }
quote = _{ "\"" }
backslash = _{"\\" }
oct = _{ '0'..'7' }
hex = _{ '0'..'9' | 'a'..'f' | 'A'..'F' }
esc = @{
    "\\" ~ ("n" | "r" | "t" | "\\" | "\'" | "\"")
  | "\\0" ~ oct ~ oct
  | "\\x" ~ hex ~ hex
  | "\\U" ~ hex{8}
  | "\\u" ~ hex{4}
}
quoted_chars = @{ (!quote ~ !backslash ~ any)+ }
quoted_string = ${ (esc | quoted_chars)* }
bare_string = @{ ('a'..'z' | 'A'..'Z' | '0'..'9' | "." | "_")+ }
parens = { "(" ~ expression ~ ")" }
function = @{ bare_string ~ "(" ~ list ~ ")" }
value = {
    function
  | bare_string
  | quote ~ quoted_string ~ quote
  | parens
  | "{" ~ dict ~ "}"
  | "[" ~ list ~ "]"
}
expression = { value ~ (infix ~ value)* }
exp_list = _{ (expression ~ sep ~ exp_list | sep ~ exp_list | expression)? }
key_value = { expression{2,} }
kv_list = _{ (key_value ~ sep ~ kv_list | sep ~ kv_list | key_value)? }
list = !{ exp_list }
dict = !{ kv_list }
root = _{ dict ~ eoi }
